plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id "java"
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
}

group = 'pl.kosma' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${project.library_name}"
version = "${project.library_version}+${project.release_date}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

dependencies {
    implementation "${project.library_maven_name}:${project.library_version}"
    implementation 'org.spigotmc:spigot-api:1.12-R0.1-SNAPSHOT'
}

processResources {
    filesMatching(['fabric.mod.json', 'mcmod.info', 'META-INF/mods.toml']) {
        expand "version": project.version
    }
}

jar {
    exclude('net')
}

shadowJar {
    dependencies {
        include(dependency("${project.library_maven_name}:.*"))
    }
    exclude('net.minecraftforge.fml.common')
}

jar.finalizedBy('shadowJar')
